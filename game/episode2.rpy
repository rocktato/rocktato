label ep_2:

    $ persistent.mainmenu_img = 2

    stop music fadeout 1.0


    space ""

    show cg 2 boyinpark with dissolve

    rt_no ""

    no "We see some red sweatered kid and a rock sitting on a park bench."

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    rt_no "Yeah.. {w=0.3}I'm sorry I got us fired at the super mart."

    rt_no "In my defense,{w=0.2} I didn't know the explosion would be that big!!"

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    rt_no "Eh... {w=0.2}those hecka rude customers deserved it."

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    rt_no "I know, {w=0.2}I know we need money."

    rt_no "But are you sure we can't just... {w=0.2}live in a cave or something?"

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    rt_no "Psh... {w=0.2}nobody NEEDS an apartment."

    rt_no "You just WANT one."

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    rt_no "Why are you so focused on BORING stuff like apartments, money, and safety?"

    rt_no "This is Beeg City!"

    no "The kid pulled up some battered up flyer."

    rt_no "'The city where your favorite superhero movies become reality!'"

    rt_no "I wanted to come here to become the coolest hero on the block! {w=0.2}To beat up bad guys! {w=0.2}To save kittens trapped in trees!"

    rt_no "Not to be a grocery CASHIER, {w=0.2}who doesn't get the basic respect they deserve."

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    rt_no "Ugh.."

    rt_no "You raise a good point..."

    rt_no "I GUUUEEESS we do need to eat."

    mr "{w=0.6}.{w=0.6}.{w=0.6}.{nw}"

    wiz_que "GREETINGS, {w=0.2}YOU IDIOTIC CITIZENS OF BEEG CITY."

    show cg 2 wizintro with dissolve

    no "The red sweatered kid looked up and saw someone standing on one of the tall towers holding a beeg megaphone."

    no "They were potato shaped and wore a weird lookin' wizard hat."

    wiz_yell_que "IT IS I, {w=0.2}WIZPOTATO!"

    wiz_yell "THE NEWEST AND COOOOOLEST VILLAIN EVER!!!"

    wiz "And I am going to take over this WHOOOOOOLEEEEE PLACE!!!!!"

    wiz "In- {w=0.2}in... a bit... {w}It's gonna take a bit.."

    wiz_yell "But COME INTO MY EVIL LAIR {w=0.2}if you dare..."

    wiz "The address is uhh... {w=0.2}this really weird shaped apartment complex I'm standing on."

    wiz "I'm in room number 7746."

    wiz "Yeah..."

    wiz "HEHEHeHEHehehehe....."

    hide cg

    show bg black

    with dissolve

    no "And with that, {w=0.2}the mysterious character retreated back into the shadows."

    no "Well... {w=0.2}he was already hiding in the shadows."

    no "But he was... {w=0.3}more?? {w=0.3}Into the shadows??"

    no "It's a flashback. {w}It's hard to rememeber these things."

    hide bg black

    show cg 2 savestuff

    with dissolve

    rt_no "..."

    rt_no "That's weird..."

    no "He glanced around the park. {w=0.2}Everyone just continued as normal."

    rt_no "No one cares?????"

    rt_no "Didn't they hear there's an evil dude out on the loose????"

    rt_no "Mr. Rock? {w=0.2}Do you know what this means???"

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    rt_no "WE GOTTA TAKE THINGS INTO OUR OWN HANDS!!!"

    rt_no "C'MON LET'S G-"

    no "Mr. Rock positioned himself on the kids's foot."

    rt_no "OW!!!"

    rt_no "DUDE??? {w=0.2}WHAT HECK??"

    rt_no "YOU WEIGH LIKE A BAJILLION POUNDS!!!"

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    rt_no "Yeah, but we gotta save stuff!!"

    rt_no "Listen, {w=0.2}it'll be quick."

    rt_no "In and out in twenty minutes, {w=0.2}promise."

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    rt_no "I mean... {w=0.2}judging from the speech and the peoples' reaction, {w=0.2}this guy (ow) doesn't seem that big of a threat."

    rt_no "So I don't see what you're so (ow) worried about."

    rt_no "Let's GO!!!!"

    no "They gently took Mr. Rock off of their foot and ran off."

    hide cg

    show bg black

    with dissolve

    no "Mr. Rock realized he couldn't stop this maniac, {w=0.2}so he just decided to follow them."


    show cg 2 apartmentclose

    rt_no ""

    mr ""

    rt_no "So uh..."

    rt_no "Are we supposed to knock? {w=0.2}Or..."

    show cg 2 apartmentopen with hpunch

    space ""

    rt_no "WOW!!!"

    rt_no "Mr. Rock. {w=0.2}AGGRESSIVE!!!"

    rt_no "I like it!"

    no "The two peeked into the doorway and saw... {w=0.2}nothing?"

    rt_no "Huh."

    no "The kid stepped into the room, {w=0.2}but Mr. Rock started rattling."

    rt_no "What you shaking for? {w=0.2}It's not like there's gonna be a really funny trap here!"

    hide cg

    show bg black

    with dissolve

    no "He took another step into the room."

    show cg 2 cagedtato

    space ""

    rt_no "Yeah... {w=0.2}I deserve that."

    rt_no "Hey, {w=0.2}get in here."

    no "Mr. Rock scootched into the cage with him."

    show cg 2 cagedtatoandwiz

    wiz "NYEHEHEHHE!!! {w=0.2}HELLO THERE YOU-"

    wiz ""

    wiz "uh.."

    wiz "Huh."

    rt_no "Hi to you too!!"

    wiz "Aw man... {w=0.2}don't take this the wrong way, but I was hoping for... {w=0.2}super... {w=0.2}guy... {w=0.2}or something."

    rt_no "Yeah, well, hate to break it to ya buddy, but none of the big heroes reeeaaallyyy... {w=0.2}cared.."

    rt_no "So uhhh..."

    wiz "Damn."

    wiz "That sucks."

    wiz "Well.. {w=0.2}you'll do!"

    wiz "You're here to try to stop me from doing evil, {w=0.2}right?"

    rt "Uh huh!!"

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    wiz "Ah- {w=0.2}you're asking for job openings???"

    wiz "I'm a villain, not a... {w=0.2}business guy..."

    wiz "Unless you wanna be an evil... {w=0.2}minion?"

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    wiz "Healthcare benefits?? {w=0.2}I don't know what that means, but sure-"

    rt_no "NOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOoooooooooooooooooooooooooo!!"

    rt_no "MR. ROCK!! {w=0.2}YOU CAN'T WORK FOR THIS EVIL GUY!!"

    rt_no "I know we're strapped for cash, {w=0.2}but MORALS DUDE!"

    rt_no "And remember, {w=0.2}we came here to BEAT UP THIS GUY!!"

    wiz "You came here to WHAT-"

    wiz "Now I WANT to hire you!!"

    wiz "Not because I want you to beat me up, {w=0.3}that's... {w=0.3}no..."

    wiz "It's so you DON'T beat me up!"

    wiz "You two can be my wacky lackies!"

    wiz "All you have to do is be funny and get insulted by me."

    wiz "Good pay, {w=0.2}trust me, {w=0.2}I'm loaded."

    mr "{w=0.6}.{w=0.6}.{w=0.6}.{nw}"

    rt_no "GASP!!! {w=0.2}NO!!"

    rt_no "I AM A HERO!! {w=0.2}A GOOD GUY!!!"

    rt_no "AND I'D NEVER HELP YOU, {w=0.2}YOU FIEND!!"

    wiz "what about the rock."

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    rt_no "NO WAY DUDE!!"

    rt_no "MR. ROCK IS MY uh... {w=0.2}sidekick!"

    mr "{w=0.6}?"

    rt_no "YEAH!"

    rt_no "You're a sidekick now"

    mr "{w=0.6}!"

    wiz "well..."

    wiz "In that case..."

    wiz "I'M GONNA beat YOU uP!!!"

    no "Wizpotato flipped a lever which dropped ANOTHER CAGE onto their cage."

    wiz ""

    wiz "I'm gonna be honest. That's all I know how to do."

    rt_no "GASP!"

    rt_no "This guy's tough.."

    rt_no "Wait.. {w=0.2}I got a plan."

    rt_no ""

    hide cg

    show bg black

    with dissolve

    no "Our hero grabbed Mr. Rock and THREW HIM AT WIZPOTATO!"

    no "The stone soared through the air, {w=0.2}like a majestic princess,{w=0.2}{nw}"

    show cg 2 wizhit with hpunch

    extend " and hit Wizpotato SQUARE IN THE FACE!"

    no "In fact, {w=0.2}he hit him so hard, {w=0.2}that the villain was BLASTED INTO THE APARTMENT WALL!"

    rt_no "WOAH!! {w=0.2}MR. ROCK!!"

    rt_no "That was awesome!!"

    rt_no ""

    rt_no "Gasp!"

    show cg 2 notdone with dissolve

    no "Wizpotato threw the rock onto the ground and got up from the concrete wall."

    no "He wasn't done yet."

    wiz "I'M NOT DONE YET!!"

    wiz "AHHHHHHHHHHHH!!!!"

    show cg whatwasithinking

    no "He kicked a broken lever towards them."

    no "It didn't hit anyone.."

    wiz ""

    rt_no ""

    wiz ""

    wiz "yeah I uh..."

    wiz "I dunno what I was thinking...."

    wiz "They were right.."

    wiz "I'm never gonna be a REAL (real) villain.."

    wiz "*sigh*"

    rt_no ""

    rt_no "Uh.."

    rt_no "Well.. {w=0.2}not with THAT attitude!"

    rt_no "You've got a lot of potential!"

    wiz "You.. {w=0.3}think?"

    rt_no "Yeah!! {w=0.2}Definitely!"

    rt_no "Uhhh.. {w=0.2}you should build on what you know right now!"

    rt_no "Maybe make the levers do more than just... {w=0.2}drop cages."

    rt_no "Maybe use lasers!"

    wiz "That's an idea!"

    no "Wizpotato grabbed his toolbox and tinkered with one of the levers."

    wiz "HAH!"

    show cg 2 whatwithlasers

    no "He kicked down the lever and many LASER GUNS APPEARED ON THE WALLS!"

    wiz "TAKE THIS!"

    rt_no "WAIT WHAT?"

    wiz "Thanks for the encouragement, {w=0.2}BUD."

    wiz "Now it seems your time being a hero is coming to an END!"

    rt_no "WELL THIS REALLY BACKFIRED HUH?"

    no "The laser charged up."

    rt_no "MR. ROCK!! {w=0.2}WHAT DO I DO?"

    rt_no "I'm still stuck in this cage and I can't get out! {w=0.2}I'm not baby-sized like you.."

    rt_no ""

    rt_no "Oh, {w=0.2}OH!!!"

    rt_no "Mr. Rock!!"

    rt_no "What if I transformed into my TRUE form?"

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    rt_no "Yeah!!"

    wiz "WHAT?"

    wiz "No WAY!"

    wiz "UGH!!! {w=0.2}IT DOESN'T MATTER! {w=0.2}FIRE!!!"

    hide cg

    show bg white

    with dissolve

    no "There was a blinding white light."

    no "All of the lasers SHOT AT OUR HERO, {w=0.2}but they just disintegrated once they touched him."

    no "The blinding white light dissapated... {w=0.2}and.."

    show cg 2 trueform

    rt_no ""

    rt_no "I'm still.. {w=0.2}working on a cool transformation sequence."

    rt_no "I don't have a lot of money right now, {w=0.2}though, {w=0.2}so..."

    rt_no "A blinding white light is all I can do..."

    rt_no "Oh.."

    rt_no "OH YEAH!! {w=0.2}HAHA!!! {w=0.2}YOUR LASERS DID S Q A U T AGAINST ME!!"

    wiz "WaahahH?? {w}You're a... {w=0.2}a.... {w=0.2}a ROCK POTATO THING!!!"

    rt_no "DING DING!!"

    rt_no "That's my funnie.. {w=0.2}funnie power!"

    rt_no "I can be a human AND whatever this.. {w=0.2}is.."

    rt_no "It's not actually that cool.."

    wiz "SHUT UP!!!"

    wiz "THAT IS MEGA SICK!!!"

    rt_no "Thanks, {w=0.2}but I'm gonna win this battle."

    rt_no "HYEA!!!"

    no "The kid kicked Wizpotato out of a wINDOW??"

    show cg 2 kickedout with hpunch

    wiz "AHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH!!!"

    wiz "My evil plans have been THWARTED!!"

    wiz "You FARTS..."

    wiz "I'LL SEE YOU AGAIN.. {w=0.2}and next time.. {w=0.2}I'LL GET MY REVENGE!!"

    rt_no "Next time??"

    wiz "I mean... {w=0.2}if you want to..."

    show cg 2 please with dissolve

    rt_no "Mr. Rock... {w=0.2}can I?? {w=0.2}pleEASE!"

    rt_no "This is the most fun I've ever had!!"

    rt_no "I'm a real hero!"

    rt_no "And I got to kick some guy out of a window with zero consequences!!"

    mr "{w=0.6}.{w=0.6}.{w=0.6}."

    no "Mr. Rock thought for a second..."

    no "When he was soaring through the air, {w=0.2}like a majestic princess, {w=0.2}he had never felt so... {w=0.2}alive."

    no "Mr. Rock shook his head approvingly."

    rt_no "YEAH!!!!!!!!"

    show cg 2 falling

    wiz "NIICE!!!"

    wiz "You're goin' down NEXT TIMEEEUUhhhhh.. {w=0.2}what's your name?"

    rt_no ""

    rt_no "Hm.."

    no "The new hero had to think."

    no "No one had ever asked him what his name was before."

    no "He didn't actually have one!"

    no "Now... he had to come up with something."

    no "Something good."

    rt_no "Let's go with..."

    rt_no "Rocktato."

    wiz "Really?"

    wiz "Rocktato sounds like... {w=0.2}if some human guy was named... {w=0.2}Boneflesh..."

    rt "Sh- {w=0.2}sHUT UP!!"

    wiz "Yeao{nw}"

    hide cg

    show bg black

    extend "OOOGGG!!"

    no ""


    # NOT FLASHBACK

    show bg citypark

    show wiz happy at left, Position(ypos=605)

    show mr at Position(xpos=1100, ypos=580)

    show rt bruh at Position(xpos=820)

    wiz "Ah... {w}those were some good times.."

    show rt exasperated

    rt "DO YOU SERIOUSLY??"

    rt "HAVE TO FLASH BACK???"

    rt "TO THE FIRST TIME WE MET???"

    show rt with hpunch

    rt "EVERY?? {w=0.2}{nw}"

    show rt with hpunch

    extend "TIME?? {w=0.2}{nw}"

    show rt with hpunch

    extend "WE DUEL???"

    show wiz sad

    wiz ""

    show wiz preach

    wiz "Yes."

    rt ""

    wiz "But anyway."

    show wiz evil 2 at bounce

    wiz "I have this entire ORPHANAGE IN A CAGE!!!"

    wiz "What are you gonna do about it?"

    show rt confused

    rt "Good.. {w=0.2}question..."

    no "Rocktato considered 'saving,' as Phrog advised."

    no "He still didn't know what it meant."

    show rt joy 2 at bounce

    rt "OKAY!!!"

    rt "I have decided..."

    show rt proud at bounce

    rt "WE'RE!! {w=0.3}Gonna BEAT YOU UP!!"

    show mr at bounce

    mr "!!!"

    $ renpy.block_rollback()

    $ saveable = False


    show rt fight idle

    show wiz fight idle

    play music "audio/music/too much effort for a one off rocktato joke but whatever lol.mp3"

    show fx battlestart at truecenter, shake(rate=0.05,strength=1,loop=2) with dissolve

    pause 1.0

    hide fx battlestart with dissolve

    $ funnywizlines = [ "", "gRAA!!!", "AGHHH!!!", "OWIE!!!", "BWAAAGG!", "SCSCSHBBJdhgakKSHkvdjhvkshkkvjdhskvjhhalvjhldvhsldvh;advh;avh;advh;;;;;;;",
    "OOOOG!!!!", "GGGRRRR!!!!!!", "", "Hhhhhhhhhhhhh", "Scskcksbakcakscbskbc *beatboxing*", "SBSHSBHCBHSB OKay...", "I'M DONE WITH THIS!"]


    $ chara_healths = [ 100, 100 ]
    $ chara_maxhealths = [ 100, 100 ]
    $ chara_hppositions = [ (200, 490), (700, 490) ]
    show screen battle_health

    $ moves = 0
    $ punch = 0
    $ mrthrow = 0


    label ep2_battle1:

        show rt fight idle at bounce
        show wiz fight idle

        $ choice_screen_type = "fight_choice"

        if moves == 13:
            jump ep2_battlelose


        menu:
            "{size=19}Epic Rocktato Super Punch!{/size}":
                $ renpy.block_rollback()

                show rt fight slap at shake(0.05, 4)
                if moves % 2 == 0:
                    show wiz hurt at shake(0.05, 4)
                else:
                    show wiz evil at bounce

                play sound "audio/fx/punch.ogg"

                rt "POW!!!"

                if punch == 0 and moves == 0:
                    $ chara_healths[0] = chara_healths[0] - 11.427
                    if chara_healths[0] < 0:
                        $ chara_healths[0] = 0

                    no "Rocktato punches Wizpotato in the face!"

                    no "Critical hit!! {w=1.0}{size=14}(I don't know what critical hit meanssss?){/size}"

                    show wiz hurt at shake(0.05, 4)

                    wiz "OOGH!!!!"

                    wiz "MY BEAUTIFUL WIZARD FACE!!!"

                    rt "HAH Haaa!"

                elif punch == 1 and moves % 2 == 1:
                    no "Wizpotato dodges!!!"

                    show rt hurt at bounce

                    rt "WHAT!!"

                    rt "YOU CAN'T DO THAT!!!"

                    show wiz smug

                    wiz "Who said not??"

                    rt "WAHHHHHHHhh!"

                elif punch == 0 and moves % 2 == 1:
                    no "Wizpotato dodges Rocktato's punch!"

                    rt "WHAT HOW??"


                elif moves % 2 == 1:
                    no "Wizpotato dodges again!!"

                    show rt hurt at bounce

                    if moves == 3:
                        wiz "NYeh!!"

                        wiz "I can do this all day!"

                    elif moves == 5:
                        wiz "Woop!"

                        wiz "Missed me!!"

                        rt "GAH!!!"

                        rt "STOP DOING THAT!"

                    elif moves == 7:
                        wiz "Caaaaan't catch me!"

                        wiz "Nyehh heee!"

                    elif moves == 9:
                        wiz "Figure it out yet?"

                        rt "AAAAAAa!"

                    elif moves == 11:
                        wiz "Wow you suck at this!!!"


                elif moves % 2 == 0:
                    $ chara_healths[0] = chara_healths[0] - 10.999
                    if chara_healths[0] < 0:
                        $ chara_healths[0] = 0
                        jump ep2_battlewin

                    if moves == 2:
                        wiz "OWWWww!!!!"

                    elif moves == 4:
                        wiz "AAAaaagggg!!!!!!"

                        wiz "Dude,,"

                    elif moves == 6:
                        wiz "SKKDSKBAbabpBPABAPBAPBapBAPB."

                        wiz "My face is gonna need some ice or something after this I stg."

                    elif moves == 8:
                        wiz "w"

                        show wiz cry at bounce

                        wiz "WAHHHHHHAHHHHHHHhhh!!"

                    elif moves == 10:
                        wiz "BBgbgbgbg"

                        wiz "Ahhahha {w=0.2}owww.."

                    elif moves == 12:
                        if chara_healths[0] < 0:
                            jump ep2_battlewin

                        else:
                            wiz "STBTTOOOPPPP!!!"

                            wiz "NOOO MOReeee!!"

                $ punch = punch + 1
                $ moves = moves + 1


            "Mr. Rock":
                $ renpy.block_rollback()

                show rt fight slap at shake(0.05, 4)
                show mr at transform_offset(x=-750, y=-120, speed=0.2)

                show wiz hurt at shake(0.05, 4)

                $ chara_healths[0] = chara_healths[0] - 4
                if chara_healths[0] < 0:
                    $ chara_healths[0] = 0
                    jump ep2_battlewin

                rt "HYEAH!!!"

                if mrthrow == 0:
                    no "Rocktato throws Mr. Rock and hits Wizpotato's face!"

                    wiz "WAHhhhhHHHhhhHHHhhhHHH!!"

                    wiz "WHY IS IT ALWAYS MY FACE!!"

                    wiz "EVERYTIME YOU HIT ME??"

                    show rt confused

                    rt "I dunno... {w=0.3}Your face is kinda your entire body.."

                    show wiz sad 2 at bounce

                    wiz "True.."

                    show wiz hurt at shake(0.05, 4)

                    wiz "HEY GET OFF ME!"

                    show mr at transform_offset(0, 0, 0)

                    wiz "Ughh"

                elif mrthrow > 0 and mrthrow < 13:
                    no "Mr. Rock hits Wizpotato's face again!"

                    $ funnywizline = funnywizlines[mrthrow]

                    wiz "[funnywizline]"

                    show mr at transform_offset(0, 0, 0)


                $ mrthrow = mrthrow + 1
                $ moves = moves + 1

            "Dance":
                $ renpy.block_rollback()

                show rt joy 2 at spinny

                show wiz sad

                rt "WEeeeeeeeeeeeeeeeeeeeeeeeeee"

                $ moves = moves + 1



        # WIZPOTATO MOVE

        if moves == 1:
            show wiz angey 2 at bounce

            if mrthrow < 1 and punch < 1:
                wiz "A"

                wiz "WHAT ARE YOU DOING??"

                wiz "Ehhh nevermind that."

                show wiz sad 2

                wiz "Uhh.."

                show wiz evil 2 at bounce

            else:
                wiz "GRmrgmrmgrm..."

                wiz "You think I can't fight back??"

                wiz "Ahaha..."

                show wiz evil 2 at bounce

                wiz "AHAHAHA!!!"

            no "He pulls a lever and it summons a giant magic fist, {w=0.3}SQUASHING ROCKTATO!"

            show rt hurt at transform_zoom(1, 0.5), transform_offset(0, -100, 0) with hpunch

            $ chara_healths[1] = chara_healths[1] - 10

            rt "GACK!!!"

            if persistent.ep1_squished == True:
                wiz "THAT'S REVENGED FOR THE SQUISHING THAT I WENT THROUGH LAST TIME!!"

                wiz "NYERH HERHEHRHERHHHH!!!"

            else:
                wiz "NYERH HERH!!!"

                wiz "Check it out!!"

                show wiz o at bounce

                wiz "It's my {w=0.3}BOY SQUISHER!"

            show rt mad at transform_zoom(1, 1), transform_offset(0, 0, 0)

            no "Rocktato unsquishes himself."

            rt "oW DUDE!!"

        elif moves == 2:
            show rt hurt at transform_zoom(1, 0.5), transform_offset(0, -100, 0) with hpunch

            $ chara_healths[1] = chara_healths[1] - 10

            rt "STOP DOING THAT????"

            show wiz smug

            wiz "nah"

            rt "M."

            show rt mad at transform_zoom(1, 1), transform_offset(0, 0, 0)

        elif moves == 3:
            show rt hurt at transform_zoom(1, 0.5), transform_offset(0, -100, 0) with hpunch

            $ chara_healths[1] = chara_healths[1] - 10

            rt "AAAGG!!"

            show wiz happy

            wiz "Hehe squish"

            show rt mad at transform_zoom(1, 1), transform_offset(0, 0, 0)

        elif moves == 4:
            show rt hurt at transform_zoom(1, 0.5), transform_offset(0, -100, 0) with hpunch

            $ chara_healths[1] = chara_healths[1] - 10

            rt "BAHHhh!"

            show wiz happy at bounce

            wiz "This is the most fun I've had in hours!"

            show rt mad at transform_zoom(1, 1), transform_offset(0, 0, 0)

        elif moves == 5:
            show rt hurt at transform_zoom(1, 0.5), transform_offset(0, -100, 0) with hpunch

            $ chara_healths[1] = chara_healths[1] - 10

            rt "BLEEhh!"

            show wiz evil 2

            wiz "NOW YOU KNOW HOW IT FEELS, {w=0.2}HAAHH!!!"

            show rt mad at transform_zoom(1, 1), transform_offset(0, 0, 0)

        elif moves == 6:
            show wiz bruh at bounce

            wiz "Okay I'm done, I'm bored with the squashy thing.."

            wiz "NEW MOVE!!!"

            wiz "UH... {w=0.2}when I come up with it."

            wiz "Yeah... {w=0.2}in a few more moves you're gonna be TOAST!!!"

        elif moves == 9:
            show rt smug at bounce

            rt "How's that 'super move' comin' along??"

            show wiz hurt at bounce

            wiz "IT'S COMIN' ALONG VERY SMOOTHLY SHUT UP!!!"

        elif moves == 11:
            show wiz evil 3

            wiz "One last move and then my super ultra secret power up uh.. {w=0.2}awesome move will be UNLEASED!!"

        elif moves == 12:
            show wiz evil 2

            wiz "YOU READY TO BE ABSOLUTELY DEMOLISHED???"


        jump ep2_battle1



    label ep2_battlewin:
        $ saveable = True

        wiz "A.."

        wiz "This is IMPOSSIBLE!!"

        show wiz screaming at bounce

        hide screen battle_health with dissolve

        wiz "I'M WIZPOTATO!!"

        wiz "THE GREATEST VILLAIN TO EVER YEAH!"

        wiz "The guy that made sentient robots JUST so they could feel the pain that I feel on a daily basis."

        wiz "THe LEVER MAN!!"

        show wiz cry

        wiz "H- {w=0.3}HOW??"

        wiz "HOW COULD THIS HAPPEN?"

        show rt joy at bounce

        rt "Because we're better than you!"

        mr "{w=2.0}."

        show rt mad

        rt "Now.."

        rt "The orphans."

        rt "Hand 'em over."

        show wiz sad

        wiz ""

        wiz "Hm."

        show wiz evil

        wiz "Hmhmhmmhmh.."

        show wiz happy

        wiz "Herh herh herh..."

        show wiz evil 2

        wiz "HERH HERH HERHHH!!"

        # FAVORITE JOKE OF THE EPISODE FJOTE

        wiz "AHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH{w}HHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH{w}H"

        show wiz smug

        wiz "Hah..."

        wiz "I do not think I will."

        show rt disgusted

        rt "What are you..."

        no "Wizpotato, {w=0.2}as a last ditch attempt, "

        extend "whipped out his gun."

        show wiz evil 2

        show rt disturbed

        rt "WOAHH!!!"

        wiz "NYERH HERH HERHHH!!!!!!"

        wiz "WHAT ARE YOU GONNA DO?"

        rt "Uhh.. {w=0.2}UHHH.."

        show rt o at bounce

        rt "OH!!"

        rt "Duh.."

        menu:
            "TRANSFORM!!!":
                $ renpy.block_rollback()
                space "{nw}"

                show fx white with dissolve

                space ""

                show rt trueform evil 2 at bounce, Position(ypos=655)

                hide fx white

                space ""

                no "Wizpotato shot!! {w=0.2}But the bullet just bounced off of Rocktato's rock-potatoey skin!"

                show rt trueform evil 3

                rt "HAH!! {w=0.2}TAKE THAT!!"

                show wiz cry at bounce

                wiz "NOOOOOOOOOOOOOOOOOOOOOOOOOO!!"

                show rt trueform smug

                rt "End of the line for YOU! {w=0.2}Villain boy.."

                show mr at bounce

                mr "!!"

                show wiz angey 2

                wiz "Ughh..."

                wiz "You..."

                show wiz angey

                wiz ""

                show wiz preach at bounce

                wiz "Well..."

                wiz "I didn't want to have to resort to this.."

                wiz "But.."

                show wiz evil 2

                no "WIZPOTATO WHIPPED OUT A ROCKET LAUNCHER!!"

                show rt trueform confused

                wiz "THERE IS NO WAY I'M LETTING YOU WIN ANOTHER TIME!!"

                wiz "SAY BYEE BYEE HERO BOYY!!!"

                show rt trueform hurt at shake(rate=0.01,strength=3,loop=4)

                rt "AH!!"

                show fx black

                $ renpy.pause(5.0, hard=True)

                space ""

                jump ep2_end




    label ep2_battlelose:
        $ saveable = True

        show wiz evil 2 at bounce

        wiz "NYEHEHEHEH!!!"

        wiz "THE TIME HAS COME!!"

        wiz "FOR YOU TO DIE!!!!"

        show wiz bruh

        wiz "Well.. not really..."

        show wiz smug at bounce

        wiz "But your HP bar is gonna go dooooooooownnnnnnnnnnnnn.."

        show rt disgusted at bounce

        rt "GASP!!"

        show wiz evil 3 at bounce

        no "The evil man flipped a lever and a GIANT RUMBOY FELL OUT OF THE SKY!!!"

        show wiz 2 onrumboy at bounce

        wiz "REMEMBER THIS THING???"

        wiz "There was NO WAY I was gonna let this bad boy go to waste!"

        wiz "MEET: {w=0.5}RUMBOY!!!"

        show rt cry 2

        rt "OH NO!!!"

        show rt happy

        rt "What does it do?"

        wiz "Watch!"

        no "He flips a lever."

        no "AND THE RUMBOY STARTS SUCKING!!!"

        show rt hurt at fast_shaking

        pause 0.01

        show mr at fast_shaking

        no "AND IT SUCKED HARD!!"

        if persistent.episode_fin == 1:
            no "That's not funny."

            no "Sh- {w=0.2}shut up."

        rt "MR. ROCK!!!"

        show mr at transform_offset(-900, -100, 0.5), transform_rotate(27, speed=0.4), transform_zoom(x=0.3, y=0.3, rate=0.4), transform_fade_out(0.9)

        no "Mr. Rock got sucked up into the Rumboy."

        rt "NOOOO!!!!"

        no "Rocktato was holding onto little blades of grass for his dear life."

        show rt disturbed

        rt "HOw do I.."

        rt "HOW DO I GET OUT OF THIS!!!??"

        rt ""

        show rt joy 2

        rt "Oh wait."

        menu:
            "TRANSFORM!!!":
                $ renpy.block_rollback()
                space "{nw}"

                show fx white with dissolve

                rt "AH HAH!!!!!"

                hide rt

                show rt trueform cool at bounce, Position(xpos=820, ypos=655)

                hide fx white

                space ""

                wiz "You idiot."

                show rt trueform o

                rt "Oh shoot."

                rt "This form doesn't have hands.."

                rt "I can't hold onto grass."

                show rt trueform cry at transform_offset(-600, -50, 0.5), transform_rotate(27, speed=0.4), transform_zoom(x=0.3, y=0.3, rate=0.4), transform_fade_out(0.9)

                rt "AHHHHHHHHHHHHHHHHHHHHHHHHhhhhhhh{size=15}hhh!{/size}"

                show fx black with dissolve

                # AUDIO WHERE HE GETS SUCKED UP AND DIES AND THEN IT BLOWS UP

                $ renpy.pause(5.0, hard=True)

                $ chara_healths[1] = chara_healths[1] - 10

                $ renpy.pause(4.3, hard=True)

                $ chara_healths[1] = chara_healths[1] - 10

                $ renpy.pause(0.2, hard=True)

                $ chara_healths[1] = chara_healths[1] - 10

                $ renpy.pause(0.3, hard=True)

                $ chara_healths[1] = chara_healths[1] - 10

                $ renpy.pause(3.2, hard=True)

                $ chara_healths[1] = chara_healths[1] - 3

                $ renpy.pause(2.6, hard=True)

                $ chara_healths[1] = chara_healths[1] - 3

                $ renpy.pause(2.1, hard=True)

                $ chara_healths[1] = chara_healths[1] - 3

                $ renpy.pause(5.0, hard=True)

                $ chara_healths[1] = chara_healths[1] - 1

                space "{w=0.3}{nw}"

                hide screen battle_health with dissolve

                space ""


    label ep2_end:
        hide fx

        hide wiz

        hide rt

        hide mr

        show bg swamp

        show ph thankful at right

        ph ""

        ph "hehe. {w=0.5}that's a funny photo."

        show ph at bounce

        ph ""

        show ph bruh

        ph ""

        ph "that isn't as funny."

        show ph at bounce

        ph ""

        show ph o 3

        ph ""

        ph "now that's funny"

        rt_que "{size=15}aaa{/size}aaaaaaAAAAAAAAAAAAAAAHHH!"

        show rt hurt at transform_rotate(degs=180.0), Position(xpos=100, ypos=300), shake(rate=0.01,strength=10,loop=4)

        show mr at transform_rotate(degs=180.0), Position(xpos=-200, ypos=500), shake(rate=0.01,strength=10,loop=3)

        show ph scared 2 at shake(rate=0.005,strength=8,loop=5)

        rt "BBBNnngg!!!"

        ph "WAHHHHH!!!!!!"

        rt "WAAAAAAAAAAAAAHHH!!!!!!!!"

        ph "WHAAAAAATT THE HELLL???"

        hide mr

        show rt happy at bounce, Position(xpos=300, ypos=0)

        show mr at Position(xpos=0, ypos=-130), bounce

        rt "O hey Phrog!!"

        show ph at shake(rate=0.005,strength=20,loop=15)

        ph "WHAAAAAAAAAT??"

        ph "WH- {w=0.3}WHERE DID YOU COME FROM???!!"

        show rt sad 2

        rt "I was fighting Wizpotato..."

        rt "And lost..."

        show ph smug

        ph "pfft lol."

        show rt cry 2

        rt "WAIT NO!!! {w=0.2}WE LOST!!!!"

        mr "{w=0.6}.{w=0.6}.{w=0.6}."

        show rt mad at bounce

        show ph o 2

        rt "Dang it!"

        show rt sad 2

        rt "Those.."

        rt "Those poor orphans..."

        show rt disgusted

        rt "They never stood a chance."

        show fx black

        space ""

        if persistent.episode_fin == 1:
            $ persistent.episode_fin = 2

        $ persistent.mainmenu_img = 3


        return()
